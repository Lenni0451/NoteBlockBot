plugins {
    id "java"
    id "checkstyle"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    archivesName = project.maven_name ?: rootProject.maven_name
    group = project.maven_group ?: rootProject.maven_group
    version = project.maven_version ?: rootProject.maven_version
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
    maven {
        name = "lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
    ivy {
        url "https://github.com"
        patternLayout {
            artifact "/[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]"
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    compileOnly "org.jetbrains:annotations:26.0.1"
    compileOnly(annotationProcessor("org.projectlombok:lombok:1.18.36"))
    include "net.lenni0451.commons:httpclient:1.6.1"
    include "net.lenni0451:optconfig:1.1.0"
    include "org.json:json:20241224"
    include("net.dv8tion:JDA:5.2.2") {
        exclude module: "opus-java"
    }
    include "org.apache.logging.log4j:log4j-slf4j2-impl:2.24.3"
    include "RaphiMC:NoteBlockTool:1.1.9@jar" //Including the entirety of NoteBlockTool is kinda cursed but it works
    include "org.xerial:sqlite-jdbc:3.47.2.0"
    include "com.bucket4j:bucket4j_jdk17-core:8.14.0"
    include "com.github.ben-manes.caffeine:caffeine:3.1.8"
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes(
                "Main-Class": "net.lenni0451.noteblockbot.Main",
                "Multi-Release": "true"
        )
    }
}

checkstyle {
    toolVersion = "10.16.0" //Latest version for Java 8: 9.3
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
}
build.dependsOn(check)
