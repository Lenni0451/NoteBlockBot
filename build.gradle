plugins {
    id "java"
    id "checkstyle"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    archivesName = project.maven_name ?: rootProject.maven_name
    group = project.maven_group ?: rootProject.maven_group
    version = project.maven_version ?: rootProject.maven_version
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
    maven {
        name = "lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
    ivy {
        url "https://github.com"
        patternLayout {
            artifact "/[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]"
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    compileOnly "org.jetbrains:annotations:26.0.1"
    compileOnly(annotationProcessor("org.projectlombok:lombok:1.18.34"))
    include "net.lenni0451.commons:httpclient:1.6.1-SNAPSHOT"
    include "org.json:json:20240303"
    include "RaphiMC:NoteBlockTool:1.1.7@jar" //Kinda cursed but it works
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes(
                "Main-Class": "net.lenni0451.noteblockworldwebhook.Main",
                "Multi-Release": "true"
        )
    }
}

checkstyle {
    toolVersion = "10.16.0" //Latest version for Java 8: 9.3
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
}
build.dependsOn(check)
